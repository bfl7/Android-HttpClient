apply plugin: 'com.android.library'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Math.max(9, Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION))
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode 10000
        versionName '1.0.0-rc1'
        //applicationId "co.tophe.ion"
    }
}

dependencies {
    def artifactSupportV4 = 'com.android.support:support-v4:21.0.3'
    def artifactIon = 'com.koushikdutta.ion:ion:2.0.5'

    def androidAsyncProject = ':AndroidAsync:AndroidAsync'
    def ionProject = ':Ion:Ion'
    def supportv4Project = ':support-v4'

    def useLocalAndroidAsync = false
    def useLocalIon = false
    def useLocalV4 = false
    allprojects {
        rootProject.allprojects.project.each {
            if (it.path == ionProject) {
                useLocalIon = true
            }
            else if (it.path == androidAsyncProject) {
                useLocalAndroidAsync = true
            }
            else if (it.path == supportv4Project) {
                useLocalV4 = true
            }
        }
    }

    if (useLocalV4) {
        println 'Using local support-v4 library in ' + project.path
        compile project(supportv4Project)
    } else {
        compile artifactSupportV4
    }

    if (useLocalIon) {
        println 'Using local Ion library in ' + project.path
        compile project(ionProject)
    } else {
        compile (artifactIon) {
            if (useLocalV4) {
                println 'Exclude support-v4 dependency for ' + artifactIon + ' in ' + project.path
                exclude group: 'com.android.support', module: 'support-v4'
            }
            if (useLocalAndroidAsync) {
                println 'Exclude androidasync dependency for ' + artifactIon + ' in ' + project.path
                exclude group: 'com.koushikdutta.async', module:'androidasync'
            }
        }
    }

    if (useLocalAndroidAsync) {
        println 'Using local AndroidAsync library in ' + project.path
        compile project(androidAsyncProject)
    }

    compile project(':Tophe')
    androidTestCompile 'com.squareup.okio:okio:1.0.1'
}

apply from: 'https://raw.githubusercontent.com/mcxiaoke/gradle-mvn-push/master/gradle-mvn-push.gradle'
